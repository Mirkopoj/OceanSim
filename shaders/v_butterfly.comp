#version 450

layout(local_size_x = 16, local_size_y = 16) in;
layout(binding = 0, rgba16f) uniform readonly image2D inImg;
layout(binding = 1, rgba16f) uniform writeonly image2D outImg;
layout(binding = 2, rgba16f) uniform readonly image2D butterfly;
layout(binding = 3) buffer readonly Stage { int stage; } stage;

vec2 comp_mul(vec2 a, vec2 b){
	return vec2(a.x * b.x - a.y * b.y, a.x * b.y + a.y * b.x);
}

void main() {	
	vec4 data = imageLoad(butterfly, ivec2(stage.stage, gl_GlobalInvocationID.y));

	vec4 p = imageLoad(inImg, ivec2(gl_GlobalInvocationID.x, data.z));
	vec4 q = imageLoad(inImg, ivec2(gl_GlobalInvocationID.x, data.w));
	vec2 w = vec2(data.x, data.y);

	vec4 res = vec4(p.rg + comp_mul(w, q.rg), p.ba + comp_mul(w, q.ba));
	imageStore(outImg, ivec2(gl_GlobalInvocationID.xy), res);
}
