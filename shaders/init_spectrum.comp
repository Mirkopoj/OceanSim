#version 450

const float PI = 3.1415926;

layout(local_size_x = 16, local_size_y = 16) in;
layout(binding = 0, rg16f) uniform writeonly image2D H0K;
layout(binding = 1, rgba16f) uniform writeonly image2D WavesData;

struct SpectrumParameters
{
	float scale;
	float angle;
	float spreadBlend;
	float swell;
	float alpha;
	float peakOmega;
	float gamma;
	float shortWavesFade;
};

layout(binding = 2) buffer readonly SpectrumParams {
	SpectrumParameters data[2];
} Spectrums;

layout(binding = 3) buffer readonly UBO {
	float LengthScale;
	float CutoffHigh;
	float CutoffLow;
	float GravityAcceleration;
	float Depth;
	uint Size;
} ubo;

float Frequency(float k, float g, float depth)
{
	return sqrt(g * k * tanh(min(k * depth, 20)));
}

float FrequencyDerivative(float k, float g, float depth)
{
	float th = tanh(min(k * depth, 20));
	float ch = cosh(k * depth);
	return g * (depth * k / ch / ch + th) / Frequency(k, g, depth) / 2;
}

float NormalisationFactor(float s)
{
	float s2 = s * s;
	float s3 = s2 * s;
	float s4 = s3 * s;
	if (s < 5)
		return -0.000564 * s4 + 0.00776 * s3 - 0.044 * s2 + 0.192 * s + 0.163;
	else
		return -4.80e-08 * s4 + 1.07e-05 * s3 - 9.53e-04 * s2 + 5.90e-02 * s + 3.93e-01;
}

float DonelanBannerBeta(float x)
{
	if (x < 0.95)
		return 2.61 * pow(abs(x), 1.3);
	if (x < 1.6)
		return 2.28 * pow(abs(x), -1.3);
	float p = -0.4 + 0.8393 * exp(-0.567 * log(x * x));
	return pow(10, p);
}

float DonelanBanner(float theta, float omega, float peakOmega)
{
	float beta = DonelanBannerBeta(omega / peakOmega);
	float sech = 1 / cosh(beta * theta);
	return beta / 2 / tanh(beta * 3.1416) * sech * sech;
}

float Cosine2s(float theta, float s)
{
	return NormalisationFactor(s) * pow(abs(cos(0.5 * theta)), 2 * s);
}

float SpreadPower(float omega, float peakOmega)
{
	if (omega > peakOmega)
	{
		return 9.77 * pow(abs(omega / peakOmega), -2.5);
	}
	else
	{
		return 6.97 * pow(abs(omega / peakOmega), 5);
	}
}

float DirectionSpectrum(float theta, float omega, SpectrumParameters pars)
{
	float s = SpreadPower(omega, pars.peakOmega)
		+ 16 * tanh(min(omega / pars.peakOmega, 20)) * pars.swell * pars.swell;
	return mix(2 / 3.1415 * cos(theta) * cos(theta), Cosine2s(theta - pars.angle, s), pars.spreadBlend);
}

float TMACorrection(float omega, float g, float depth)
{
	float omegaH = omega * sqrt(depth / g);
	if (omegaH <= 1)
		return 0.5 * omegaH * omegaH;
	if (omegaH < 2)
		return 1.0 - 0.5 * (2.0 - omegaH) * (2.0 - omegaH);
	return 1;
}

float JONSWAP(float omega, float g, float depth, SpectrumParameters pars)
{
	float sigma;
	if (omega <= pars.peakOmega)
		sigma = 0.07;
	else
		sigma = 0.09;
	float r = exp(-(omega - pars.peakOmega) * (omega - pars.peakOmega)
		/ 2 / sigma / sigma / pars.peakOmega / pars.peakOmega);
	
	float oneOverOmega = 1 / omega;
	float peakOmegaOverOmega = pars.peakOmega / omega;
	return pars.scale * TMACorrection(omega, g, depth) * pars.alpha * g * g
		* oneOverOmega * oneOverOmega * oneOverOmega * oneOverOmega * oneOverOmega
		* exp(-1.25 * peakOmegaOverOmega * peakOmegaOverOmega * peakOmegaOverOmega * peakOmegaOverOmega)
		* pow(abs(pars.gamma), r);
}

float ShortWavesFade(float kLength, SpectrumParameters pars)
{
	return exp(-pars.shortWavesFade * pars.shortWavesFade * kLength * kLength);
}


vec2 UniformToGaussian(float u1, float u2) {
    float R = sqrt(-2.0f * log(u1));
    float theta = 2.0f * PI * u2;

    return vec2(R * cos(theta), R * sin(theta));
}

float hash(uint n) {
    n = (n << 13U) ^ n;
    n = n * (n * n * 15731U + 0x789221U) + 0x76312589U;
    return float(n & uint(0x7fffffffU)) / float(0x7fffffff);
}

void main() {
	ivec2 id = ivec2(gl_GlobalInvocationID.xy);
	float deltaK = 2 * PI / ubo.LengthScale;
	int nx = id.x - int(ubo.Size) / 2;
	int nz = id.y - int(ubo.Size) / 2;
	vec2 k = vec2(nx, nz) * deltaK;
	float kLength = length(k);

	uint seed = id.x + ubo.Size * id.y + ubo.Size;
	
	if (kLength <= ubo.CutoffHigh && kLength >= ubo.CutoffLow)
	{
		float kAngle = atan(k.y, k.x);
		float omega = Frequency(kLength, ubo.GravityAcceleration, ubo.Depth);
		imageStore(WavesData, id, vec4(k.x, 1 / kLength, k.y, omega));
		float dOmegadk = FrequencyDerivative(kLength, ubo.GravityAcceleration, ubo.Depth);

		float spectrum = JONSWAP(omega, ubo.GravityAcceleration, ubo.Depth, Spectrums.data[0])
			* DirectionSpectrum(kAngle, omega, Spectrums.data[0]) * ShortWavesFade(kLength, Spectrums.data[0]);
		if (Spectrums.data[1].scale > 0)
			spectrum += JONSWAP(omega, ubo.GravityAcceleration, ubo.Depth, Spectrums.data[1])
			* DirectionSpectrum(kAngle, omega, Spectrums.data[1]) * ShortWavesFade(kLength, Spectrums.data[1]);

		seed += uint(hash(seed) * 10);
      vec4 uniformRandSamples = vec4(hash(seed), hash(seed * 2), hash(seed * 3), hash(seed * 4));
      vec2 gauss = vec2(
				UniformToGaussian(uniformRandSamples.z, uniformRandSamples.w).x,
				UniformToGaussian(uniformRandSamples.x, uniformRandSamples.y).y
			);
		imageStore(H0K, id, vec4(vec2(gauss.x, gauss.y) * sqrt(2 * spectrum * abs(dOmegadk) / kLength * deltaK * deltaK), 0, 1));
	}
	else
	{
		imageStore(H0K, id, vec4(0,0,0,1));
		imageStore(WavesData, id, vec4(k.x, 1, k.y, 1));
	}
}
